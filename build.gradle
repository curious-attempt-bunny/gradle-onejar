apply plugin: 'groovy'
apply plugin: 'eclipse'

version = '0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	groovy 'org.codehaus.groovy:groovy-all:1.7.6'
}

sourceSets {
	example {
		groovy {
			srcDir 'src/example/groovy'
		}
	}
}

task exampleJar(type: Jar) {
	appendix = 'example'
	from sourceSets.example.classes
}

task exampleOneJar(type: OneJar) {
	appendix = 'exampleOneJar'
	mainClass = 'com.curiousattemptbunny.onejar.ExampleMain'
	mainJar = exampleJar
	runtime = runtime  // optional
}

artifacts {
  archives exampleOneJar
}

// TODO --- the rest will hopefully become a plugin:

apply plugin: 'java'

// TODO this will need to move to become a dependency
sourceSets {
	thirdparty {
		java {
			srcDir 'src/thirdparty/java'	
		}
	}
}

class OneJar extends Jar {
	@Input
	String mainClass
	// TODO @Input @Optional
	Configuration runtime
	// TODO @Input @Optional
	Jar mainJar
	
	OneJar() {
		// default settings		
		mainJar = project.tasks.jar
		runtime = project.configurations.runtime

		// more defaults		
		configure {
			appendix = 'onejar'
			manifest {
			    	attributes  'Created-By':'Gradle OneJar task',
					    'Main-Class':'com.simontuffs.onejar.Boot'
			}
		}

		// post-configuration action
		copyAction.rootSpec.addChild().into('') {
			from { project.sourceSets.thirdparty.classes }
		}

		// post-configuration action
		copyAction.rootSpec.addChild().into('main') {
			from { mainJar.archivePath }
			rename { 'main.jar' }
		}

		// post-configuration action
		copyAction.rootSpec.addChild().into('lib') {
			from { runtime }
		}
	}

	public void setMainClass(String mainClass) {
		this.mainClass = mainClass
		configure {
			manifest {
				attributes 'One-Jar-Main-Class': mainClass
			}
		}	
	}

	public void setMainJar(Jar mainJar) {
		this.mainJar = mainJar
		this.dependsOn mainJar // TODO this needs to work for the default settings too
	}
}
