apply plugin: 'groovy'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

dependencies {
	groovy 'org.codehaus.groovy:groovy-all:1.7.6'
}

sourceSets {
	thirdparty {
		java {
			srcDir 'src/thirdparty/java'	
		}
	}
}

task exampleOneJar(type: OneJar, dependsOn: jar) {
// TODO	mainClass = 'com.curiousattemptbunny.onejar.ExampleMain'
}

artifacts {
  archives exampleOneJar
}

class OneJar extends Jar {
	String mainClass = 'com.curiousattemptbunny.onejar.ExampleMain'
	
	OneJar() {
		def sourceSets = project.sourceSets
		def configurations = project.configurations
		def jar = project.tasks.jar
		
		configure {
			appendix = 'onejar'
			manifest {
		    	attributes  'Created-By':'Gradle OneJar task',
							'Main-Class':'com.simontuffs.onejar.Boot',
							'One-Jar-Main-Class': mainClass
			}
		
			from sourceSets.thirdparty.classes
		
			into('lib') {
				from configurations.runtime	
			}
		
			into('main') {
				from jar.archivePath
				rename { 'main.jar' }
			}
		}
	}
}